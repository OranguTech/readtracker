apply plugin: 'com.android.application'

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:1.3.0'
    }
}

repositories {
    mavenCentral()
    maven { url 'https://github.com/christoffer/readmill-java-wrapper/raw/master/snapshots' }
}

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.2'

    testOptions {
        unitTests.returnDefaultValues = true
    }

    signingConfigs {
        release {
            /*
               Values below should be defined in: ~/.gradle/gradle.properties
               READTRACKER_RELEASE_STORE_FILE={path to your keystore}
               READTRACKER_RELEASE_STORE_PASSWORD=*****
               READTRACKER_RELEASE_KEY_ALIAS=*****
               READTRACKER_RELEASE_KEY_PASSWORD=*****
             */

            // Let the presences of READTRACKER_RELEASE_STORE_FILE serve as an indicator that
            // gradle.properties is setup properly.
            if(project.hasProperty('READTRACKER_RELEASE_STORE_FILE')) {
                storeFile file(READTRACKER_RELEASE_STORE_FILE)
                storePassword READTRACKER_RELEASE_STORE_PASSWORD
                keyAlias READTRACKER_RELEASE_KEY_ALIAS
                keyPassword READTRACKER_RELEASE_KEY_PASSWORD
            } else {
                throw new RuntimeException("Gradle configuration not properly setup for signing. " +
                        "See app/build.gradle for more info")
            }
        }
    }

    useLibrary 'org.apache.http.legacy'

    defaultConfig {
        minSdkVersion 10
        targetSdkVersion 23
        versionCode 18
        versionName "3.2"
        testApplicationId "com.readtracker.android.test"
    }

    lintOptions {
        abortOnError false
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt'
            signingConfig signingConfigs.release
        }

        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
        }
    }

    lintOptions {
        abortOnError false
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }

    sourceSets {
        test {
            resources {
                srcDirs = ['src/test/assets']
            }
        }
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:support-v4:23.1.1'
    compile 'com.android.support:palette-v7:23.1.1'
    compile 'com.j256.ormlite:ormlite-android:4.48'
    compile 'com.j256.ormlite:ormlite-core:4.48'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.squareup:otto:1.3.8'
    compile 'com.jakewharton:butterknife:4.0.1'
    compile files('lib/annotations.jar')
    testCompile 'org.skyscreamer:jsonassert:1.2.3'
    testCompile 'junit:junit:4.12'
}
